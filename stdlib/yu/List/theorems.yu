import basic => yu/basic
| Empty
| (...#Empty)
| Bool
| (...#Bool)
| (=#)
| refl
| (...#Equal.basic)
| flip
| (...#Ty)

import L => yu/List/List
| (...)

import relation? => yu/relation?/relation?
| (...#Bool)

import Ord => yu/Order/Order
| Order
| (...#Order)

import Ord-th => yu/Order/theorems
| (...#Order)

import Eq? => yu/Equivalence?/Equivalence?
| Equivalence?
| (...#Equivalence?)

import Ord? => yu/Ordering?/Ordering?
| Ordering?
| (...#Ordering?)

import Ord?-th => yu/Ordering?/theorems
| (...#Ordering?)


val (.nilLeftIdentityOp:+:#List) [A : Ty]
  : (xs : List A) -> (nil :+: xs) = xs
let _ => refl


val (.nilRightIdentityOp:+:#List) [A : Ty]
  : (xs : List A) -> (xs :+: nil) = xs
let nil => refl
let (x :: xs) =>
  xs .nilRightIdentityOp:+: .ap (t => x :: t)


val (.associativeOp:+:#List) [A : Ty]
  : (xs : List A) & (ys : List A) & (zs : List A) ->
    (xs :+: (ys :+: zs)) = ((xs :+: ys) :+: zs)
let nil _ _ => refl
let (x :: xs) ys zs =>
  xs .associativeOp:+: ys zs .ap (t => x :: t)


val (.is::Op:+:Left#List) [A : Ty] :
  (xs : List A) & (ys : List A) & xs .Is:: -> (xs :+: ys) .Is::
let (x :: xs) ys is:: => is::


val (.is::Map#List) [A : Ty] [B : Ty] :
  (xs : List A) & (f : A -> B) & xs .Is:: -> xs .map f .Is::
let (x :: xs) f is:: => is::


val (.simplRevOp::Aux#List) [A : Ty] :
  (xs ys : List A) ->
  xs .accum ys (flip (::)) = xs .accum nil (flip (::)) :+: ys
let nil ys => refl
let (x :: xs) ys =>
  xs .simplRevOp::Aux (x :: nil) .rewR (t => t :+: ys) of
  xs .simplRevOp::Aux (x :: ys)
  @ xs .accum nil (flip (::)) .associativeOp:+: (x :: nil) ys


val (.simplRevOp::#List) [A : Ty] :
  (xs : List A) & (x : A) -> (x :: xs) .rev = xs .rev :+: x :: nil
let xs x => xs .simplRevOp::Aux (x :: nil)


val (.revDistributesOp:+:#List) [A : Ty] :
  (xs ys : List A) -> (xs :+: ys) .rev = ys .rev :+: xs .rev
let nil ys => ! ys .rev .nilRightIdentityOp:+:
let (x :: xs) ys =>
  (xs :+: ys) .simplRevOp:: x @
  xs .revDistributesOp:+: ys .rewL (t => t :+: x :: nil) of
  xs .simplRevOp:: x .rewR (t => ys .rev :+: t) of
  ! ys .rev .associativeOp:+: (xs .rev) (x :: nil)


val (.op:+:PreservesAll#List) [A : Ty] [P : A -> Ty] :
  (xs ys : List A) & xs .All P & ys .All P ->
  (xs :+: ys) .All P
let nil ys allNil Ys => Ys
let (x :: xs) ys (X :: Xs) Ys => X :: xs .op:+:PreservesAll ys Xs Ys


val (.revPreservesAll#List) [A : Ty] [P : A -> Ty] :
  (xs : List A) & xs .All P -> xs .rev .All P
let nil H => allNil
let (x :: xs) (X :: Xs) =>
  xs .simplRevOp:: x .rew(t => t .All P) of
  xs .rev .op:+:PreservesAll
    (x :: nil) (xs .revPreservesAll Xs) (X :: allNil)


val cmpIsTransitive?[A : Ty] :
  (C : Ordering? A) -> cmpToLe?.Ord? (s t => s .cmp#List t C) .IsTransitive?
let C => isTrans
where
  val R : List A & List A -> Bool
  let => cmpToLe?.Ord? (s t => s .cmp#List t C)

  val isTrans : (xs ys zs : List A) & ? R xs ys & ? R ys zs -> ? R xs zs
  let nil _ nil _ _ => refl
  let nil _ (_ :: _) _ _ => refl
  let (_ :: _) nil _ {} _
  let (_ :: _) (_ :: _) nil _ {}
  let (x :: xs) (y :: ys) (z :: zs) lxys lyzs =>
    aux (C .cmp x y) (C .cmp y z) (C .cmp x z)
      cmpxyGtIsEmpty cmpyzGtIsEmpty cmpxyEqToGt cmpyzEqToGt
      composeEqEq composeEqLt composeLtEq composeLtLt
  where
    val aux :
      (xy yz xz : Order) &
      (xy = gt.Ord -> Empty) &
      (yz = gt.Ord -> Empty) &
      (xy = eq.Ord -> ? ! xs .cmp ys C .gt?) &
      (yz = eq.Ord -> ? ! ys .cmp zs C .gt?) &
      (? xy .eq? & ? yz .eq? -> xz = eq.Ord) &
      (? xy .eq? & ? yz .lt? -> xz = lt.Ord) &
      (? xy .lt? & ? yz .eq? -> xz = lt.Ord) &
      (? xy .lt? & ? yz .lt? -> xz = lt.Ord) ->
      ? ! xz .rec lt.Ord (xs .cmp zs C) gt.Ord .gt?
    let gt.Ord _ _ c _ _ _ _ _ _ _ => c refl .rec
    let _ gt.Ord _ _ c _ _ _ _ _ _ => c refl .rec
    let eq.Ord eq.Ord xz _ _ exy eyz e _ _ _ =>
      e refl refl .rewL
        (a => ! a .rec lt.Ord (xs .cmp zs C) gt.Ord .gt?) of
      isTrans xs ys zs (exy refl) (eyz refl)
    let eq.Ord lt.Ord xz _ _ _ _ _ e _ _ =>
      e refl refl .rewL
          (a => ! a .rec lt.Ord (xs .cmp zs C) gt.Ord .gt?) refl
    let lt.Ord eq.Ord xz _ _ _ _ _ _ e _ =>
      e refl refl .rewL
          (a => ! a .rec lt.Ord (xs .cmp zs C) gt.Ord .gt?) refl
    let lt.Ord lt.Ord xz _ _ _ _ _ _ _ e =>
      e refl refl .rewL
          (a => ! a .rec lt.Ord (xs .cmp zs C) gt.Ord .gt?) refl

    val cmpxyGtIsEmpty : C .cmp x y = gt.Ord -> Empty
    let e =>
      e .rew
        (c => ? ! c .rec lt.Ord (xs .cmp ys C) gt.Ord .gt? -> Empty)
        (a => case a | {} end)
        lxys

    val cmpyzGtIsEmpty : C .cmp y z = gt.Ord -> Empty
    let e =>
      e .rew
        (c => ? ! c .rec lt.Ord (ys .cmp zs C) gt.Ord .gt? -> Empty)
        (a => case a | {} end)
        lyzs

    val cmpxyEqToGt : C .cmp x y = eq.Ord -> ? ! xs .cmp ys C .gt?
    let e =>
      e .resp (c => ? ! c .rec lt.Ord (xs .cmp ys C) gt.Ord .gt?) lxys

    val cmpyzEqToGt : C .cmp y z = eq.Ord -> ? ! ys .cmp zs C .gt?
    let e =>
      e .resp (c => ? ! c .rec lt.Ord (ys .cmp zs C) gt.Ord .gt?) lyzs

    val composeEqEq :
      ? C .cmp x y .eq? & ? C .cmp y z .eq? -> C .cmp x z = eq.Ord
    let eqxy eqyz =>
      eqxy' := C .cmpEqIsEq? x y .resp (?) eqxy;
      eqyz' := C .cmpEqIsEq? y z .resp (?) eqyz;
      t := C .eqIsTransitive? x y z eqxy' eqyz';
      C .cmp x z .eq?ToOp= (C .cmpEqIsEq? x z .rew (?) t)

    val composeEqLt :
      ? C .cmp x y .eq? & ? C .cmp y z .lt? -> C .cmp x z = lt.Ord
    let eqxy ltyz =>
      eqxy' := C .cmpEqIsEq? x y .resp (?) eqxy;
      ltyz' := C .cmpLtIsLt? y z .resp (?) ltyz;
      t := C .leLtToLt? x y z (C .eqToLe x y eqxy') ltyz';
      C .cmp x z .lt?ToOp= (C .cmpLtIsLt? x z .rew (?) t)

    val composeLtEq :
      ? C .cmp x y .lt? & ? C .cmp y z .eq? -> C .cmp x z = lt.Ord
    let ltxy eqyz =>
      ltxy' := C .cmpLtIsLt? x y .resp (?) ltxy;
      eqyz' := C .cmpEqIsEq? y z .resp (?) eqyz;
      t := C .ltLeToLt? x y z ltxy' (C .eqToLe y z eqyz');
      C .cmp x z .lt?ToOp= (C .cmpLtIsLt? x z .rew (?) t)

    val composeLtLt :
      ? C .cmp x y .lt? & ? C .cmp y z .lt? -> C .cmp x z = lt.Ord
    let ltxy ltyz =>
      ltxy' := C .cmpLtIsLt? x y .resp (?) ltxy;
      ltyz' := C .cmpLtIsLt? y z .resp (?) ltyz;
      t := C .ltIsTransitive? x y z ltxy' ltyz';
      C .cmp x z .lt?ToOp= (C .cmpLtIsLt? x z .rew (?) t)
  end
end


val cmpIsCompatible [A : Ty] :
  (C : Ordering? A) -> (xs ys : List A) ->
  xs .cmp ys C .lt? = ys .cmp xs C .gt?
let C => isCompat
where
  val isCompat :
    (xs ys : List A) ->
    xs .cmp ys C .lt? = ys .cmp xs C .gt?
  let nil nil => refl
  let (_ :: _) nil => refl
  let nil (_ :: _) => refl
  let (x :: xs) (y :: ys) =>
    aux (C .cmp x y) (C .cmp y x)
      (ltToGt x y) (gtToLt x y) (eqToEq x y)
  where
    val aux :
      (xy yx : Order) &
      (? xy .lt? -> yx = gt.Ord) &
      (? xy .gt? -> yx = lt.Ord) &
      (? xy .eq? -> yx = eq.Ord) ->
      xy .rec lt.Ord (xs .cmp ys C) gt.Ord .lt?
      = yx .rec lt.Ord (ys .cmp xs C) gt.Ord .gt?
    let lt.Ord yx e _ _ =>
      e refl .rewR
        (a => a .rec lt.Ord (ys .cmp xs C) gt.Ord .gt?) refl
    let gt.Ord yx _ e _ =>
      e refl .rewR
        (a => a .rec lt.Ord (ys .cmp xs C) gt.Ord .gt?) refl
    let eq.Ord yx _ _ e =>
      e refl .rewR
        (a => a .rec lt.Ord (ys .cmp xs C) gt.Ord .gt?) (isCompat xs ys)

    val ltToGt : (x y : A) & ? C .cmp x y .lt? -> C .cmp y x = gt.Ord
    let x y ltxy =>
      C .cmp y x .gt?ToOp= (C .cmpLtIsGt? x y .resp (?) ltxy)

    val gtToLt : (x y : A) & ? C .cmp x y .gt? -> C .cmp y x = lt.Ord
    let x y gtxy =>
      C .cmp y x .lt?ToOp= (C .cmpLtIsGt? y x .rew (?) gtxy)

    val eqToEq : (x y : A) & ? C .cmp x y .eq? -> C .cmp y x = eq.Ord
    let x y eqxy =>
      C .cmp y x .ind
        (a => (? a .lt? -> Empty) & (? a .gt? -> Empty) -> a = eq.Ord)
        (f _ => f refl .rec) (_ _ => refl) (_ g => g refl .rec)
        contraLt
        contraGt
    where
      val contraLt : ! ? C .cmp y x .lt?
      let ltyx =>
        case ! ltToGt y x ltyx @ C .cmp x y .eq?ToOp= eqxy
        | {}
        end

      val contraGt : ! ? C .cmp y x .gt?
      let gtyx =>
        case ! gtToLt y x gtyx @ C .cmp x y .eq?ToOp= eqxy
        | {}
        end
    end
  end
end


val ordering? [A : Ty] : (C : Ordering? A) -> Ordering? (List A)
let C =>
  fromCmp.Ord? (s t => s .cmp t C)
    (cmpIsTransitive? C) (cmpIsCompatible C)


val equivalence? [A : Ty] : (C : Ordering? A) -> Equivalence? (List A)
let C => ordering? C .equivalence?
