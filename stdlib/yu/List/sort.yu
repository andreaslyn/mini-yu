import L => yu/List/List
of List
of nil
of (...#List)

import basic => yu/basic
of Bool
of false
of true

import DL1 => yu/DelayList1/DelayList1
of DelayList1
of (...#DelayList1)

import Ord? => yu/Ordering?/Ordering?
of Ordering?
of (...#Ordering?)


val (.sort#List) [A : Ty] : List A & Ordering? A -> List A
let origList cmp => sequences origList .tournament merge
where
  val sequences : List A -> DelayList1 (List A)
  let (a :: b :: xs) =>
    case cmp .le? a b
    of false => descending b (a :: nil) xs
    of true => ascending b (as => a :: as) xs
    end
  let xs => one.DL1 xs

  val descending : A & List A & List A -> DelayList1 (List A)
  let a as (b :: bs) =>
    case cmp .le? a b
    of false => descending b (a :: as) bs
    of true => (a :: as) :: at _ => sequences (b :: bs)
    end
  let a as bs => (a :: as) :: at _ => sequences bs

  val ascending : A & (List A -> List A) & List A -> DelayList1 (List A)
  let a as (b :: bs) =>
    case cmp .le? a b
    of true => ascending b (ys => as (a :: ys)) bs
    of false => as (a :: nil) :: at _ => sequences (b :: bs)
    end
  let a as bs => as (a :: nil) :: at _ => sequences(bs)

  val merge : List A & List A -> List A
  let (a :: as) (b :: bs) =>
    case cmp .le? a b
    of false => b :: merge (a :: as) bs
    of true => a :: merge as (b :: bs)
    end
  let nil bs => bs
  let as nil => as
end
