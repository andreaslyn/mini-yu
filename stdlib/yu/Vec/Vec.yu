import N => yu/Nat
of Nat
of 0
of (...#Nat)

import B => yu/basic/Bool
of Bool
of (...#Bool)


data Vec : Nat & Ty -> Ty
of nil [A] : Vec 0 A
of (::#Vec) [A n] : A & Vec n A -> Vec (++ n) A


val (.head#Vec) [A] [n : Nat] : Vec (++ n) A -> A
let (x :: _) => x


val (.tail#Vec) [A] [n : Nat] : Vec (++ n) A -> Vec n A
let (_ :: xs) => xs


val (.accum#Vec) [A B n] : Vec n B & A & (A & B -> A) -> A
let nil a _ => a
let (x :: xs) a f => xs .accum (f a x) f


val (.map#Vec) [A B n] : Vec n A & (A -> B) -> Vec n B
let nil _ => nil
let (x :: xs) f => f x :: xs .map f


val (.index#Vec) [A n] : Vec n A & (i : Nat) & ? (i <? n) -> A
let nil 0 {}
let nil (++ _) {}
let (x :: xs) 0 _ => x
let (_ :: xs) (++ i) L => xs .index i L
